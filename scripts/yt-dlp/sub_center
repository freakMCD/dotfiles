#!/usr/bin/env fish

argparse -n yt-compat 'h/help' 's/embed-subs' -- $argv
or return

if set -q _flag_help
    echo "Usage: yt_compat [OPTIONS] <url>"
    echo "Options:"
    echo "  -s, --embed-subs    Burn Spanish/English subtitles"
    echo "  -h, --help          Show help"
    return
end

if test (count $argv) -ne 1
    echo "Error: Requires exactly 1 argument (URL)"
    return 1
end

set url "$argv[1]"
set temp_dir (mktemp -d)

# Nix-safe filename generation
set video_title (yt-dlp --get-title -- "$url" | string replace -ra '[^a-zA-Z0-9]' '_' | string sub -l 50)
set output_name "$video_title"(date +"_%Y%m%d")"-center.mp4"

# Download best 480p version
yt-dlp -f "bestvideo[height<=480]+bestaudio" --cookies-from-browser firefox \
    --merge-output-format mp4 \
    --sub-langs "es,en" --write-subs --convert-subs srt \
    -o "$temp_dir/video.mp4" \
    -- "$url"

# Find subtitle file with priority
set sub_file ""
if set -q _flag_embed_subs
    for lang in es en
        set sub_file "$temp_dir/video.$lang.srt"
        if test -f "$sub_file"
            break
        else
            set sub_file ""
        end
    end
end

# Always process through FFmpeg for compatibility
if set -q _flag_embed_subs; and test -n "$sub_file"
    ffmpeg -hide_banner -i "$temp_dir/video.mp4" \
        -vf "curves=m='0/0 0.45/0.3 1/0.6',subtitles='$sub_file':force_style='Fontsize=24,Fontname=DejaVu Sans,Alignment=10,MarginV=360,PrimaryColour=&H00FFFFFF,OutlineColour=&H00000000,BorderStyle=1,Outline=1,Shadow=1,Bold=1,Italic=1',drawtext=font='DejaVu Sans':text='EdwinAlexV':fontcolor=white@0.5:fontsize=24:x=w-tw-20:y=h-th-10:shadowcolor=black:shadowx=2:shadowy=2" \
        -profile:v baseline -level 3.0 \
        -preset fast \
        -crf 26 \
        -pix_fmt yuv420p \
        -c:v libx264 \
        -c:a aac -q:a 0.9 \
        -movflags +faststart \
        -y "$output_name"
else
    # Process without subtitles but maintain compatibility settings
    ffmpeg -hide_banner -i "$temp_dir/video.mp4" \
        -profile:v main -level 4.0 \
        -preset medium \
        -crf 26 \
        -pix_fmt yuv420p \
        -c:v libx264 \
        -c:a aac -q:a 0.9 \
        -movflags +faststart \
        -y "$output_name"
end

# Cleanup
rm -rf "$temp_dir"
echo "Created: "(realpath "$output_name")
